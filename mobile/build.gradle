// module :mobile
plugins {
    id 'com.android.application'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    compileSdk = rootProject.ext.get('compileSdk')
    namespace = rootProject.ext.get('applicationId')
    defaultConfig {
        applicationId = rootProject.ext.get('applicationId')
        versionName = rootProject.ext.get('versionName')
        versionCode = rootProject.ext.get('versionCode')
        targetSdk = rootProject.ext.get('targetSdk')
        minSdk = rootProject.ext.get('minSdk')
        manifestPlaceholders = [ MAPS_API_KEY: "", crashlyticsEnabled: false ]
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        testBuildType = "debug"
        multiDexEnabled = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    dependenciesInfo {
        includeInBundle = true
        includeInApk = true
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            // signingConfig signingConfigs.debug
            testCoverageEnabled = false
            minifyEnabled = false
            debuggable = true
            jniDebuggable = true
            pseudoLocalesEnabled = false
            shrinkResources = false
        }

        release {
            // signingConfig signingConfigs.release
            proguardFile("${project.rootDir}/proguard/default.pro")
            testCoverageEnabled = false
            minifyEnabled = true
            shrinkResources = true
            pseudoLocalesEnabled = false
            jniDebuggable = false
            debuggable = false
        }
    }
}

dependencies {

    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.preference) {
        exclude(group: "androidx.lifecycle", module: "lifecycle-viewmodel-ktx")
    }

    /*
     * Google Play Services: Location & Maps
     * https://issuetracker.google.com/issues/327611582
     */
    implementation(libs.bundles.google.play.services)

    /* jUnit */
    testImplementation(libs.junit)

    /* Required for connected tests. */
    debugImplementation(libs.androidx.test.monitor)

    androidTestImplementation(libs.bundles.androidx.test)
}
